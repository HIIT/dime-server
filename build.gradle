buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.2.RELEASE")
 	classpath("org.liquibase:liquibase-gradle-plugin:1.1.1")
	classpath("org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final")
	classpath("mysql:mysql-connector-java:5.1.37")
      }	
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'org.liquibase.gradle'

jar {
    baseName = 'dime-server'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("com.h2database:h2")
    compile("mysql:mysql-connector-java:5.1.37")
    compile("com.fasterxml.jackson.core:jackson-annotations")
    compile("org.apache.lucene:lucene-core:5.3.0")
    compile("org.apache.lucene:lucene-analyzers-common:5.3.0")
    compile("org.apache.lucene:lucene-queryparser:5.3.0")
    compile("org.liquibase.ext:liquibase-hibernate4:3.5")
    compile("org.liquibase:liquibase-core:3.4.1")
    compile("org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    //testCompile("junit:junit")
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

task buildDocker(dependsOn: jar, type: Exec) {
    commandLine 'docker', 'build', '-t', 'dime-server', '.'
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

liquibase {
  activities {
    main {
      changeLogFile 'src/main/resources/db/changelog/db.changelog-master.xml'
      url 'jdbc:mysql://localhost:3306/dime'
      username 'dime'
      password ''
      referenceDriver 'liquibase.ext.hibernate.database.connection.HibernateDriver'
      referenceUrl 'hibernate:spring:fi.hiit.dime.data?dialect=org.hibernate.dialect.MySQLDialect'
    }
  }
  runList = 'main'
}
